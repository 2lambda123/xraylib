buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0'
    }
}

plugins {
    id 'com.adarshr.test-logger' version '1.7.0'
    id 'maven-publish'

    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'biz.aQute.bnd.builder'


version = '@VERSION@'
group = 'com.github.tschoonj'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

libsDirName = '..'


repositories {
    mavenCentral()
}

dependencies {
	api 'org.apache.commons:commons-math3:3.6.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

sourceSets {
	main {
		java {
			srcDir '@srcdir@'
			exclude 'tests/**'
		}
		resources {
			srcDir '@builddir@'
			include 'xraylib.dat'
		}
	}
	test {
		java {
			srcDir '@srcdir@/tests'
		}
	}
}

jar {
	manifest {
		attributes('Bundle-Vendor': 'Tom Schoonjans',
			'Bundle-SymbolicName': 'com.github.tschoonj.xraylib',
			'Bundle-Name': project.name,
			'Export-Package': 'com.github.tschoonj.xraylib',
			'-noee': true
		)
	}
}

task checkCompile(type: JavaCompile) {
	source = fileTree('@top_srcdir@/example/') 
	include 'xrlexample7.java'
	classpath = sourceSets.main.runtimeClasspath
	options.sourcepath = files('@top_srcdir@/example')
	destinationDir = file('.')
}


task checkRun(dependsOn: 'checkCompile', type: JavaExec) {
	main = 'xrlexample7'
	classpath = sourceSets.main.runtimeClasspath + files('.')
}

test {
	useJUnitPlatform()
}


task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java 
            artifact sourcesJar
            artifact javadocJar
                        
            pom {
                name = 'xraylib'
                description = 'A library for X-ray matter interaction cross sections for X-ray fluorescence applications'
                url = 'https://github.com/tschoonj/xraylib'
                licenses {
                    license {
                        name = '3-Clause BSD License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'tschoonj'
                        name = 'Tom Schoonjans'
                        email = 'Tom.Schoonjans@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/tschoonj/xraylib.git'
                    developerConnection = 'scm:git:git://github.com/tschoonj/xraylib.git'
                    url = 'https://github.com/tschoonj/xraylib.git'
                }
            }
        }
    }
}

bintray {
    user = 'tschoonj'
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'xraylib'
        userOrg = 'tschoonj'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/tschoonj/xraylib.git'
        version {
            name = project.version
            released  = new Date()
        }
    }
    // Upload the 'maven' publication i.e jar, sourcesJar, javadocJar, POM
    publications = ['maven']
}
