buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0'
    }
}

plugins {
    id 'com.adarshr.test-logger' version '1.7.0'
}

apply plugin: 'java'
apply plugin: 'biz.aQute.bnd.builder'


version = '@VERSION@'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

libsDirName = '..'


repositories {
    mavenCentral()
}

dependencies {
	compile 'org.apache.commons:commons-math3:3.6.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

sourceSets {
	main {
		java {
			srcDir '@srcdir@'
			exclude 'tests/**'
		}
		resources {
			srcDir '@builddir@'
			include 'xraylib.dat'
		}
	}
	test {
		java {
			srcDir '@srcdir@/tests'
		}
	}
}

jar {
	manifest {
		attributes('Bundle-Vendor': 'Tom Schoonjans',
			'Export-Package': 'com.github.tschoonj.xraylib',
			'-noee': true
		)
	}
}

task checkCompile(type: JavaCompile) {
	source = fileTree('@top_srcdir@/example/') 
	include 'xrlexample7.java'
	classpath = sourceSets.main.runtimeClasspath
	options.sourcepath = files('@top_srcdir@/example')
	destinationDir = file('.')
}


task checkRun(dependsOn: 'checkCompile', type: JavaExec) {
	main = 'xrlexample7'
	classpath = sourceSets.main.runtimeClasspath + files('.')
}

test {
	useJUnitPlatform()
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
}
