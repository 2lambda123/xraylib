;Copyright (c) 2010, Tom Schoonjans
;All rights reserved.

;Redistribution and use in source and binary forms, with or without
;modification, are permitted provided that the following conditions are met:
;    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
;    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
;    * The names of the contributors may not be used to endorse or promote products derived from this software without specific prior written permission.

;THIS SOFTWARE IS PROVIDED BY Tom Schoonjans ''AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL Tom Schoonjans BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

;Written by Tom Schoonjans

!define PRODUCT_NAME "xraylib"
!define VERSION @VERSION@
!define PRODUCT_VERSION @VERSION@
!define PRODUCT_WEB_SITE "http://github.com/tschoonj/xraylib"
!define PRODUCT_DIR_REGKEY "Software\xraylib"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define abs_top_srcdir_win "C:\MinGW\msys\1.0\@abs_top_srcdir_win@"
!define abs_top_builddir_win "C:\MinGW\msys\1.0\@abs_top_builddir_win@" 
!define DLLTOOL "i686-pc-mingw32-dlltool"

!addincludedir "${abs_top_srcdir_win}\nsis" 
!include "MUI2.nsh"
!include "Library.nsh"
!include "EnvVarUpdate.nsh"
!include "Sections.nsh"
!include "WordFunc.nsh"
!include "FileFunc.nsh"

!macro unix2dos my_file extension
        ${GetFileName} ${my_file} $R0
        File ${my_file}
        Push $R0
        Push "temp.file"
        Call _unix2dos
        Rename "temp.file" "$R0${extension}"
!macroend



;!define MUI_LANGDLL_REGISTRY_ROOT "HKLM"
;!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_DIR_REGKEY}"
;!define MUI_LANGDLL_REGISTRY_VALUENAME "Language"

Name "xraylib"
OutFile xraylib-${VERSION}-win32.exe
RequestExecutionLevel none

InstallDir "$PROGRAMFILES\xraylib"

!ifdef NSIS_LZMA_COMPRESS_WHOLE
SetCompressor lzma
!else
SetCompressor /SOLID lzma
!endif

SetOverwrite ifnewer
CRCCheck on

!define MUI_ABORTWARNING

;!define MUI_WELCOMEPAGE_TITLE "xraylib: an open-source library for X-ray -- matter interactions"
!define MUI_WELCOMEPAGE_TEXT "Thank you for downloading version ${VERSION} of xraylib. This wizard will assist you in installing the necessary libraries and headers to start your own development"
!define MUI_WELCOMEPAGE_TITLE_3LINES
!insertmacro MUI_PAGE_WELCOME
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "${abs_top_srcdir_win}\nsis\License.rtf"
!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_TEXT "Thank you for installing xraylib. Remember: the latest version of xraylib can be obtained from the development repository on github"
!define MUI_FINISHPAGE_LINK "xraylib development link"
!define MUI_FINISHPAGE_LINK_LOCATION "http://github.com/tschoonj/xraylib"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README.txt"
!insertmacro MUI_PAGE_FINISH


!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH


Section "xraylib core files" SecCore
	SectionIn RO
	SetAutoClose false
	SetOutPath "$INSTDIR"
	DetailPrint "Installing documentation"
	;Readme etc
	!insertmacro unix2dos "${abs_top_srcdir_win}\README" ".txt"
	!insertmacro unix2dos "${abs_top_srcdir_win}\AUTHORS" ".txt"
	!insertmacro unix2dos "${abs_top_srcdir_win}\BUGS" ".txt"
	!insertmacro unix2dos "${abs_top_srcdir_win}\Changelog" ".txt"
	!insertmacro unix2dos "${abs_top_srcdir_win}\TODO" ".txt"

	;Documentation
	SetOutPath "$INSTDIR\Doc"
	!insertmacro unix2dos "${abs_top_srcdir_win}\doc\xraybanner.txt" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\doc\xraydoc.txt" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\doc\xrayfunc.txt" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\doc\xrayhelp.txt" ""



	DetailPrint "Installing core library..."
	;copy dll to C:\WINDOWS\system32 or something similar
	SetOutPath "$INSTDIR\Lib"
	!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED ${abs_top_builddir_win}\src\.libs\libxrl-@LIB_CURRENT_MINUS_AGE@.dll $SYSDIR\libxrl-@LIB_CURRENT_MINUS_AGE@.dll "$SYSDIR"

		

	;install the redistributable Visual C++ libraries
	SetOutPath "$INSTDIR\temp"
	File "${abs_top_srcdir_win}\..\vcredist_x86_100.exe"
	File "${abs_top_srcdir_win}\..\vcredist_x86_90.exe"
	ExecWait "$INSTDIR\temp\vcredist_x86_100.exe /q"
	ExecWait "$INSTDIR\temp\vcredist_x86_90.exe /q"
	SetOutPath "$INSTDIR"
	RMDir /r "$INSTDIR\temp"
	



	WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" $INSTDIR
	WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "DisplayName" "xraylib ${VERSION}"
	WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "DisplayVersion" "${VERSION}"
	WriteRegDWORD HKLM ${PRODUCT_UNINST_KEY} "NoModify" 1
	WriteRegDWORD HKLM ${PRODUCT_UNINST_KEY} "NoRepair" 1
	WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
	WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"
	WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

Section /o "xraylib SDK" SecSDK
	SetAutoClose false
	SetShellVarContext all
	
	;copy lib file to Lib for development...
	DetailPrint "Installing static libraries..."
	SetOutPath "$INSTDIR\Lib"
	File ${abs_top_builddir_win}\nsis\libxrl-@LIB_CURRENT_MINUS_AGE@.lib
	File ${abs_top_builddir_win}\nsis\libxrl-@LIB_CURRENT_MINUS_AGE@-bcc.lib
	File ${abs_top_builddir_win}\nsis\libxrl-@LIB_CURRENT_MINUS_AGE@.exp
	File ${abs_top_builddir_win}\src\.libs\libxrl.a
	File ${abs_top_builddir_win}\src\.libs\libxrl.dll.a
	File ${abs_top_builddir_win}\src\libxrl-@LIB_CURRENT_MINUS_AGE@.def
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\README" ".txt"

	;Examples
	DetailPrint "Installing examples..."
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample1.c" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample6.cpp" ""

	;Headers
	DetailPrint "Installing headers..."
	SetOutPath "$INSTDIR\Include"
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-parser.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-lines.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-shells.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-auger.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-crystal-diffraction.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-defs.h" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\include\xraylib-nist-compounds.h" ""

SectionEnd

Section /o ".NET bindings" SecdotNet
	SetOutPath "$INSTDIR\Lib"
	!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED ${abs_top_srcdir_win}\nsis\dotNet\XrayLib.NET.dll $INSTDIR\Lib\XrayLib.NET.dll "$INSTDIR"
	
	;Documentation
	SetOutPath "$INSTDIR\Doc"
	File ${abs_top_srcdir_win}\nsis\dotNet\XrayLib.NET.chm
	
	;Example
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample8.cs" ""
SectionEnd

Section /o "IDL bindings" SecIDL

	DetailPrint "Installing IDL example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample4.pro" ""

	DetailPrint "Installing IDL bindings"
	SetOutPath "$INSTDIR\dlm"
	File ${abs_top_builddir_win}\idl\.libs\libxrlidl.dll
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\libxrlidl.dlm" ""
	${EnvVarUpdate} $0 "IDL_DLM_PATH" "P" "HKLM" "<IDL_DEFAULT>"
	${EnvVarUpdate} $0 "IDL_DLM_PATH" "A" "HKLM" "$INSTDIR\dlm"

	SetOutPath "$INSTDIR\pro"
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib.pro" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib_help.pro" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib_lines.pro" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib_shells.pro" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib_auger.pro" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\idl\xraylib_nist_compounds.pro" ""
	${EnvVarUpdate} $0 "IDL_PATH" "P" "HKLM" "<IDL_DEFAULT>"
	${EnvVarUpdate} $0 "IDL_PATH" "A" "HKLM" "$INSTDIR\pro"

SectionEnd

SectionGroup /e "Python bindings" SecPython

Section "Python 2.6" SecPython26

	DetailPrint "Installing Python example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample5.py" ""

	;python bindings
	DetailPrint "Installing Python 2.6 bindings"
	SetOutPath "$INSTDIR\Python"
	File ${abs_top_builddir_win}\nsis\python\python2.6\_xraylib.pyd

	;python executable
	!insertmacro unix2dos "${abs_top_builddir_win}\nsis\xraylib.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xrayhelp.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraymessages.py" ""

	SetOutPath "$INSTDIR\Bin"
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraylib-cli.py" ""
	${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"
	${EnvVarUpdate} $0 "PYTHONPATH" "A" "HKLM" "$INSTDIR\Python"
SectionEnd

Section /o "Python 2.7" SecPython27

	DetailPrint "Installing Python example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample5.py" ""

	;python bindings
	DetailPrint "Installing Python 2.7 bindings"
	SetOutPath "$INSTDIR\Python"
	File ${abs_top_builddir_win}\nsis\python\python2.7\_xraylib.pyd

	;python executable
	!insertmacro unix2dos "${abs_top_builddir_win}\nsis\xraylib.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xrayhelp.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraymessages.py" ""

	SetOutPath "$INSTDIR\Bin"
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraylib-cli.py" ""
	${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"
	${EnvVarUpdate} $0 "PYTHONPATH" "A" "HKLM" "$INSTDIR\Python"
SectionEnd

Section /o "Python 3.1" SecPython31

	DetailPrint "Installing Python example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample5.py" ""

	;python bindings
	DetailPrint "Installing Python 3.1 bindings"
	SetOutPath "$INSTDIR\Python"
	File ${abs_top_builddir_win}\nsis\python\python3.1\_xraylib.pyd

	;python executable
	!insertmacro unix2dos "${abs_top_builddir_win}\nsis\xraylib.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xrayhelp.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraymessages.py" ""

	SetOutPath "$INSTDIR\Bin"
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraylib-cli.py" ""
	${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"
	${EnvVarUpdate} $0 "PYTHONPATH" "A" "HKLM" "$INSTDIR\Python"
SectionEnd

Section /o "Python 3.2" SecPython32

	DetailPrint "Installing Python example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample5.py" ""

	;python bindings
	DetailPrint "Installing Python 3.2 bindings"
	SetOutPath "$INSTDIR\Python"
	File ${abs_top_builddir_win}\nsis\python\python3.2\_xraylib.pyd

	;python executable
	!insertmacro unix2dos "${abs_top_builddir_win}\nsis\xraylib.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xrayhelp.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraymessages.py" ""

	SetOutPath "$INSTDIR\Bin"
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraylib-cli.py" ""
	${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"
	${EnvVarUpdate} $0 "PYTHONPATH" "A" "HKLM" "$INSTDIR\Python"
SectionEnd

Section /o "Python 3.3" SecPython33

	DetailPrint "Installing Python example"
	SetOutPath "$INSTDIR\Example"
	!insertmacro unix2dos "${abs_top_srcdir_win}\example\xrlexample5.py" ""

	;python bindings
	DetailPrint "Installing Python 3.3 bindings"
	SetOutPath "$INSTDIR\Python"
	File ${abs_top_builddir_win}\nsis\python\python3.3\_xraylib.pyd

	;python executable
	!insertmacro unix2dos "${abs_top_builddir_win}\nsis\xraylib.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xrayhelp.py" ""
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraymessages.py" ""

	SetOutPath "$INSTDIR\Bin"
	!insertmacro unix2dos "${abs_top_srcdir_win}\nsis\python\cli\xraylib-cli.py" ""
	${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"
	${EnvVarUpdate} $0 "PYTHONPATH" "A" "HKLM" "$INSTDIR\Python"
SectionEnd
SectionGroupEnd

LangString DESC_SecCore ${LANG_ENGLISH} "Installs the xraylib core dll. Required"
LangString DESC_SecSDK ${LANG_ENGLISH} "Installs the headers and static libraries for linking against xraylib"
LangString DESC_SecdotNet ${LANG_ENGLISH} "Installs the xraylib .Net/C# bindings"
LangString DESC_SecIDL ${LANG_ENGLISH} "Installs the xraylib IDL bindings"
LangString DESC_SecPython ${LANG_ENGLISH} "Installs the xraylib Python bindings"

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${SecCore} $(DESC_SecCore)
!insertmacro MUI_DESCRIPTION_TEXT ${SecSDK} $(DESC_SecSDK)
!insertmacro MUI_DESCRIPTION_TEXT ${SecdotNet} $(DESC_SecdotNet)
!insertmacro MUI_DESCRIPTION_TEXT ${SecIDL} $(DESC_SecIDL)
!insertmacro MUI_DESCRIPTION_TEXT ${SecPython} $(DESC_SecPython)
!insertmacro MUI_FUNCTION_DESCRIPTION_END



Section "Uninstall"
	Delete "$INSTDIR\Include\xraylib.h"
	Delete "$INSTDIR\Include\xraylib-parser.h"
	Delete "$INSTDIR\Include\xraylib-auger.h"
	Delete "$INSTDIR\Include\xraylib-crystal-diffraction.h"
	Delete "$INSTDIR\Include\xraylib-defs.h"
	Delete "$INSTDIR\Include\xraylib-lines.h"
	Delete "$INSTDIR\Include\xraylib-shells.h"
	Delete "$INSTDIR\Include\xraylib-nist-compounds.h"
;	Delete "$INSTDIR\Lib\libxrl-3.dll.lnk"
;	Delete "$SYSDIR\libxrl-3.dll"
	!insertmacro UnInstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED $SYSDIR\libxrl-@LIB_CURRENT_MINUS_AGE@.dll
	!insertmacro UnInstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED $INSTDIR\Lib\XrayLib.NET.dll
	Delete "$INSTDIR\Doc\xraybanner.txt"
	Delete "$INSTDIR\Doc\xraydoc.txt"
	Delete "$INSTDIR\Doc\xrayfunc.txt"
	Delete "$INSTDIR\Doc\xrayhelp.txt"
	Delete "$INSTDIR\Doc\XrayLib.NET.chm"
	Delete "$INSTDIR\Example\xrlexample1.c"
	Delete "$INSTDIR\Example\xrlexample6.cpp"
	Delete "$INSTDIR\Example\xrlexample5.py"
	Delete "$INSTDIR\Example\xrlexample5.pyc"
	Delete "$INSTDIR\Example\xrlexample4.pro"
	Delete "$INSTDIR\Example\xrlexample8.cs"
	Delete $INSTDIR\Lib\libxrl-@LIB_CURRENT_MINUS_AGE@.lib
	Delete $INSTDIR\Lib\libxrl-@LIB_CURRENT_MINUS_AGE@-bcc.lib
	Delete $INSTDIR\Lib\libxrl-@LIB_CURRENT_MINUS_AGE@.exp
	Delete $INSTDIR\Lib\libxrl-@LIB_CURRENT_MINUS_AGE@.def
	Delete "$INSTDIR\Lib\libxrl.dll.a"
	Delete "$INSTDIR\Lib\libxrl.a"
	Delete "$INSTDIR\Lib\README.txt"
	Delete "$INSTDIR\Python\_xraylib.pyd"
	Delete "$INSTDIR\Python\xraymessages.py"
	Delete "$INSTDIR\Python\xrayhelp.py"
	Delete "$INSTDIR\Python\xraylib.py"
	Delete "$INSTDIR\Python\xraymessages.pyc"
	Delete "$INSTDIR\Python\xrayhelp.pyc"
	Delete "$INSTDIR\Python\xraylib.pyc"
	Delete "$INSTDIR\Bin\xraylib-cli.py"
	Delete "$INSTDIR\Bin\xraylib-cli.pyc"
	Delete "$INSTDIR\dlm\libxrlidl.dll"
	Delete "$INSTDIR\dlm\libxrlidl.dlm"
	Delete "$INSTDIR\pro\xraylib.pro"
	Delete "$INSTDIR\pro\xraylib_help.pro"
	Delete "$INSTDIR\pro\xraylib_shells.pro"
	Delete "$INSTDIR\pro\xraylib_lines.pro"
	Delete "$INSTDIR\pro\xraylib_auger.pro"
	Delete "$INSTDIR\pro\xraylib_nist_compounds.pro"

	RMDir "$INSTDIR\Include"
	RMDir "$INSTDIR\Lib"
	RMDir "$INSTDIR\Doc"
	RMDir "$INSTDIR\Example"
	RMDir /r "$INSTDIR\Python"
	RMDir "$INSTDIR\Bin"
	RMDir "$INSTDIR\pro"
	RMDir "$INSTDIR\dlm"
	Delete "$INSTDIR\README.txt"
	Delete "$INSTDIR\AUTHORS.txt"
	Delete "$INSTDIR\BUGS.txt"
	Delete "$INSTDIR\Changelog.txt"
	Delete "$INSTDIR\TODO.txt"
	Delete "$INSTDIR\Uninstall.exe"
	RMDir "$INSTDIR"

	${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR\Bin"
	${un.EnvVarUpdate} $0 "PYTHONPATH" "R" "HKLM" "$INSTDIR\Python"
	${un.EnvVarUpdate} $0 "IDL_DLM_PATH" "R" "HKLM" "$INSTDIR\dlm"
	${un.EnvVarUpdate} $0 "IDL_PATH" "R" "HKLM" "$INSTDIR\pro"
	DeleteRegKey /ifempty HKLM "${PRODUCT_DIR_REGKEY}"
	DeleteRegKey HKLM ${PRODUCT_UNINST_KEY}
SectionEnd
Function _unix2dos
    ; strips all CRs
    ;     ; and then converts all LFs into CRLFs
    ;     ; (this is roughly equivalent to "cat file | dos2unix | unix2dos")
    ;     ;
    ;     ; usage:
    ;     ;    Push "infile"
    ;     ;    Push "outfile"
    ;     ;    Call unix2dos
    ;     ;
    ;     ; beware that this function destroys $0 $1 $2
          ClearErrors
    
          Pop $2
          FileOpen $1 $2 w 

          Pop $2
          FileOpen $0 $2 r
  
          Push $2 ; save name for deleting
    
          IfErrors unix2dos_done
   
      ; $0 = file input (opened for reading)
      ; $1 = file output (opened for writing)

      unix2dos_loop:
      ; read a byte (stored in $2)
      FileReadByte $0 $2
      IfErrors unix2dos_done ; EOL
      ; skip CR
      StrCmp $2 13 unix2dos_loop
      ; if LF write an extra CR
      StrCmp $2 10 unix2dos_cr unix2dos_write
   
      unix2dos_cr:
       FileWriteByte $1 13
    
      unix2dos_write:
      ; write byte
      FileWriteByte $1 $2
      ; read next byte
      Goto unix2dos_loop
 
      unix2dos_done:
      ; close files
      FileClose $0
      FileClose $1
    
      ; delete original
      Pop $0
      Delete $0
   
FunctionEnd

Function .onInit
	ReadRegStr $R0 HKLM ${PRODUCT_UNINST_KEY} "UninstallString"
	StrCmp $R0 "" done
	MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "${PRODUCT_NAME} is already installed. $\n$\nClick `OK` to remove the previous version or `Cancel` to cancel this upgrade." IDOK uninst
	Abort

	uninst:
	ClearErrors
	Exec $INSTDIR\Uninstall.exe

	done:
	StrCpy $1 ${SecPython26}

FunctionEnd

Function .onSelChange
  	!insertmacro StartRadioButtons $1
 		!insertmacro RadioButton ${SecPython26}
 		!insertmacro RadioButton ${SecPython27}
 		!insertmacro RadioButton ${SecPython31}
 		!insertmacro RadioButton ${SecPython32}
 		!insertmacro RadioButton ${SecPython33}
  	!insertmacro EndRadioButtons

FunctionEnd

!insertmacro MUI_LANGUAGE "English"
